plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "org.moditect.gradleplugin" version "1.0.0-rc3"
}

apply from: "$rootDir/deps/java.gradle"
apply from: "$rootDir/deps/junit.gradle"
apply from: 'publish.gradle'
apply from: "$rootDir/deps/publish-base.gradle"

version = file('../version').text
group = 'io.xpipe'
archivesBaseName = 'api'

repositories {
    mavenCentral()
}

dependencies {
    // Fix warnings about missing annotations
    compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "2.13.0"
    testCompileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "2.13.0"

    implementation project(':core')
    implementation project(':beacon')
}

def canTestWithDev = findProject(':app') != null
def daemonCommand = canTestWithDev ? "cmd.exe /c \"$rootDir\\gradlew.bat\" :app:run" : 'xpipe.exe'

test {
    workingDir = rootDir

    // Daemon properties
    systemProperty "io.xpipe.beacon.exec", daemonCommand +
            " -Dio.xpipe.app.mode=tray" +
            " -Dio.xpipe.beacon.port=21722" +
            " -Dio.xpipe.app.dataDir=$projectDir/local/" +
            " -Dio.xpipe.storage.persist=false" +
            " -Dio.xpipe.app.writeSysOut=true" +
            // " -Dio.xpipe.beacon.debugOutput=true" +
            " -Dio.xpipe.app.logLevel=trace"

    // API properties
    // systemProperty 'io.xpipe.beacon.debugOutput', "true"
    // systemProperty 'io.xpipe.beacon.debugExecOutput', "true"
    systemProperty "io.xpipe.beacon.port", "21722"
}
